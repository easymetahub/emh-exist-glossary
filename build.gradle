import org.apache.tools.ant.filters.BaseFilterReader
import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.xqdoc:xqdoc:1.9.9"
    }
}

repositories {
  jcenter()
}

class XQDocFilter extends BaseFilterReader {
    XQDocFilter(Reader input) {
        super(new StringReader(new org.xqdoc.ExistDBProcessor().process(input.text)))
    }
}

/**
 * Polymer 
 */

task npmInstallBase(type: Exec) {
    String npm = 'npm';
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        npm = 'npm.cmd'
    }
    workingDir 'src/main/polymer/base'
    commandLine npm, 'install'
}

task polymerBuildBase(type: Exec) {
    String polymer = 'polymer';
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        polymer = 'polymer.cmd'
    }
    workingDir 'src/main/polymer/base/'
    commandLine polymer, 'build'
}

task copyPolymerBase(type: Copy) {
  into 'build/default'
  from 'src/main/polymer/base/build/default'
}

task npmInstallAdmin(type: Exec) {
    String npm = 'npm';
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        npm = 'npm.cmd'
    }
    workingDir 'src/main/polymer/admin'
    commandLine npm, 'install'
}

task polymerBuildAdmin(type: Exec) {
    String polymer = 'polymer';
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        polymer = 'polymer.cmd'
    }
    workingDir 'src/main/polymer/admin/'
    commandLine polymer, 'build'
}

task copyPolymerAdmin(type: Copy) {
  into 'build/default/admin'
  from 'src/main/polymer/admin/build/default'
}

task copyModules(type: Copy) {
  into 'build/default'
  from 'src/main/xquery'
}

task copyResources(type: Copy) {
  into 'build/default'
  from 'src/main/resources'
}

task generateXQDocs(type: Copy) {
  into 'xqDoc'
  from 'src/main/xquery'
  include '**/*.xq*'
  rename { it - '.xq*' + '.xml' } 
  includeEmptyDirs = false
  eachFile { println it }
  filter XQDocFilter
}

def xqDocTree = fileTree(dir:'xqDoc', excludes:['openapi.json'])
task fsTeardownXQDocs(type: Delete) {
  delete xqDocTree
}

task buildXAR(type: Zip) {
   from 'build/default/'
   include '*'
   include '*/**' //to include contents of a folder present inside build/default directory
   archiveName 'emh-glossary-0.8.0.xar'
   destinationDir(file('build/'))
}



// Task dependencies
buildXAR.dependsOn copyModules
buildXAR.dependsOn copyResources
// Base polymer page
buildXAR.dependsOn copyPolymerBase
copyPolymerBase.dependsOn polymerBuildBase
polymerBuildBase.dependsOn npmInstallBase
// Admin polymer page
buildXAR.dependsOn copyPolymerAdmin
copyPolymerAdmin.dependsOn polymerBuildAdmin
polymerBuildAdmin.dependsOn npmInstallAdmin



generateXQDocs.mustRunAfter(fsTeardownXQDocs)

